set dotenv-required := true
set dotenv-load := true

host_target := if os() == "macos" { "aarch64-apple-darwin" } else if os() == "windows" { "x86_64-pc-windows-msvc" } else { "x86_64-unknown-linux-gnu" } 

host_excludes := "sketch nrf-solar-monitor"
host_excludes_cli := prepend('--exclude ', host_excludes)

default: build test clippy size

ci: build test clippy

build: build_components build_nrf

clean: clean_components clean_nrf

clippy: clippy_components clippy_nrf

test: test_components

size: size_nrf

build_components:
    cargo build
    cargo build --features log
    cargo build --features defmt
    cargo build --release --features defmt --target thumbv7em-none-eabihf

clippy_components:
    cargo clippy
    cargo clippy --features log
    cargo clippy --features defmt
    cargo clippy --release --features defmt --target thumbv7em-none-eabihf

clean_components:
    cargo clean

[working-directory: 'nrf']
build_nrf:
    cargo build --release 

[working-directory: 'nrf']
clippy_nrf:
    cargo clippy --release
    
[working-directory: 'nrf']
clean_nrf:
    cargo clean

test_components:
    cargo test --features log

[working-directory: 'nrf']
size_nrf:
    cargo size --release --bin nrf-solar-monitor 

[working-directory: 'nrf']
run:
    cargo run --release --bin nrf-solar-monitor

[working-directory: 'nrf']
attach:
    probe-rs attach --chip nRF52840_xxAA target/thumbv7em-none-eabihf/release/nrf-solar-monitor 

[working-directory: 'nrf']
sketch bin:
    echo "Running sketch app {{bin}}"
    cargo run --release --bin {{bin}}

  